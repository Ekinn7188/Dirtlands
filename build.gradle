//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'nu.studer.jooq' version '6.0.1'
    id "org.flywaydb.flyway" version "8.0.5"
    id "xyz.jpenilla.run-paper" version "1.0.6"
}

group 'dirtlands.net'

shadowJar{
    archiveFileName = "${project.name}.jar"
}

repositories {
    mavenCentral()
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven {
        name = "sonatype-oss-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://repo.citizensnpcs.co/" }
    maven { url "https://repo.codemc.io/repository/maven-snapshots/" }
    mavenLocal()
}

dependencies {
    //minecraft
    implementation "net.kyori:adventure-text-minimessage:4.2.0-SNAPSHOT"
    compileOnly 'net.luckperms:api:5.3'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'
    implementation 'org.reflections:reflections:0.10.2'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.28-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    implementation 'jeeper.utils:PaperPluginUtils:0.1.1'
    implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'

    //database
    implementation 'org.jooq:jooq:3.15.4'
    compileOnly 'org.xerial:sqlite-jdbc:3.36.0.3'
    implementation 'org.flywaydb:flyway-core:8.1.0'
    implementation 'ch.qos.logback:logback-classic:1.2.8'
    jooqGenerator 'org.xerial:sqlite-jdbc:3.36.0.3'

    //other
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.16.0'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.16.0'
}

flyway {
    url = "jdbc:sqlite:${buildDir}/generate-source.db"
//    locations = ['classpath:db/migration']
}

jooq {
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = "jdbc:sqlite:${buildDir}/generate-source.db"
                }
                generator {
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                        excludes = 'flyway_schema_history|sqlite_master|sqlite_sequence'
                    }
                    //                generate {}
                    target {
                        packageName = 'dirtlands.db'
//                        directory = '/database'
                    }
                }
            }
        }
    }
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.17.1")
    }
}

task cleanGenerateSourceDB(type: Delete) {
    delete "${buildDir}/generate-source.db"
}

flywayMigrate.dependsOn(cleanGenerateSourceDB)
generateJooq.dependsOn(flywayMigrate)
classes.dependsOn(generateJooq)

task cleanServer(type: Delete) {
    delete "${projectDir}/dirtlands.db"
    delete "${projectDir}/run/usercache.json"
    delete "${projectDir}/run/plugins/Dirtlands/dirtlands.db"
    delete "${projectDir}/run/plugins/Dirtlands/config.yml"
    delete files("${projectDir}/run/world/playerdata")
}

task emptyConfig(type: Delete) {
    delete "${projectDir}/run/plugins/Dirtlands/config.yml"
}

